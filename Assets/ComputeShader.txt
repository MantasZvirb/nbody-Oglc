#version 420
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable
layout (local_size_x = 32,local_size_y = 1) in;

struct ObjectData
{
	vec4 s;//Location
	vec4 v;//Speed
	float q;//Charge
	float m;//Mass
	float r;//radius
	float rf;//Repulsion force
};

layout(std430,binding = 2) buffer Pb
{
	ObjectData Dta[];
};

uniform float dt;//delta t
uniform float sr;//system sphere radius

const float drag = 0.4;

void main()
{
	//barrier();
	vec3 fv = vec3(0,0,0);//force vector
	uint indx = gl_GlobalInvocationID.x;
	uint Pn = gl_NumWorkGroups.x*32;
	for(uint i = 0;i < Pn;i++)
	{
		if(i != indx)
		{
			vec3 Distv = Dta[indx].s.xyz - Dta[i].s.xyz;
			vec3 ndist = normalize(Distv);
			float Dist = length(Distv);
			float r = Dta[indx].r;
			float Force = 100*(pow(((r+1)/Dist),12) - 2*(pow(((r+1)/Dist),6)));
			fv += ndist*Force;
		}
	}
	//drag force vector = speedVector*dragConstant
	fv -= Dta[indx].v.xyz*drag;

	float pm = Dta[indx].m;//particle mass	
	vec3 av = fv/pm;//acceleration vector
	vec3 vv = av*dt;//velocity vector
	Dta[indx].v.xyz += vv;//add to particle speed

	barrier();//wait till every kernel reaches this point
	vec3 oldPos = Dta[indx].s.xyz;
	Dta[indx].s.xyz += Dta[indx].v.xyz*dt;

	if(length(vec3(Dta[indx].s.xyz)) > sr)
	{
		Dta[indx].s.xyz = oldPos;
		Dta[indx].v.xyz *= -1;
	}

}
